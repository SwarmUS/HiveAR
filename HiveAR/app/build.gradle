import groovy.json.JsonSlurper

plugins {
    id 'com.android.application'
    id "com.google.protobuf" version "0.8.14"
    id "de.undercouch.download" version "4.1.1"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.swarmus.hivear"
        minSdkVersion 28
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task downloadDirectoryGitHub {
    doLast {
        project.delete(files("$projectDir/src/main/proto"))
        // download directory listing via GitHub API
        def dir = 'https://api.github.com/repos/SwarmUS/Pheromones/contents/hivemind-host/proto/'
        def contentsFile = new File(buildDir, "directory_contents.json")
        download {
            src dir
            dest contentsFile
        }

        // parse directory listing
        def contents = new JsonSlurper().parseFile(contentsFile, "utf-8")
        def urls = contents.collect { it.download_url }

        // download files
        download {
            src urls
            dest "$projectDir/src/main/proto"
        }

        // delete downloaded directory listing
        contentsFile.delete()
    }
}

defaultTasks 'downloadDirectoryGithub'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.github.felHR85:UsbSerial:6.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.google.protobuf:protobuf-javalite:3.14.0'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

// Dependencies from https://nimblehq.co/public/downloads/android-bangkok-conference-2020-protobuf-android-nimble.pdf
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.14.0"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}